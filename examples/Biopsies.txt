	Biopsies: discrete variable
		 latent class model

Spiegelhalter and Stovin (1983) presented data on repeated biopsies of transplanted hearts, in which a total of 414 biopsies had been taken at 157 sessions.  Each biopsy was graded on evidence of rejection using a  4 category scale of none (O), minimal (M), mild (+) and  moderate-severe (++).  Part of the data is shown below.


	Combination	Multinomial response	Session frequency
	_______________________________________________________
	  O  O	(2, 0, 0, 0)	12
	  M  M  O	(1, 2, 0, 0)	10
	  +  +   O	(1, 0, 2, 0)	17
	 ++ ++ ++	(0, 0, 0, 3)	5

The sampling procedure may not detect the area of maximum rejection, which is considered the true underlying state at the time of the session and denoted ti --- the underlying probability distribution  of the four true states is denoted by the vector p. It is then assumed that each of the observed biopsies are conditionally independent given this truestate with the restriction that there are no`false positives': i.e. one cannot observe a biopsy worse than the true state.  We then have the sampling model	

	bi  ~  Multinomial(eti, ni)

	ti  ~  Categorical(p)


where bi denotes the multinomial response at session i where ni biopsies have been taken, and ejk is the probability that a true state ti = j generates a biopsy in state k.The no-false-positive restriction means that e12 = e13 = e14 = e23 = e24 = e34 = 0. Spiegelhalter and Stovin (1983) estimated the parameters ej and p using the EM algorithm, with some smoothing to avoid zero estimates.

The appropriate graph is shown below, where the role of the true state ti is simply to pick the appropriate row from the 4 x 4 error matrix e. Here  the probability vectors ej (j = 1,...,4) and p are assumed to have uniform priors on the unit simplex, which correspond to Dirichlet priors with all parameters being 1.

The BUGS code for this model is given below. No initial values are provided for the latent states, since the forward sampling procedure will find a configuration of starting values that is compatible with the expressed constraints. We also note the apparent ``cycle'' in the graph created by the expression nbiops[i] <- sum(biopsies[i,]). This will lead Such ``cycles'' are permitted provided that they are only data transformation statements, since this does not affect the essential probability model. 


	model
	{
		for (i in 1 : ns){
			nbiops[i] <- sum(biopsies[i, ])  
			true[i]  ~ dcat(p[])
			biopsies[i, 1 : 4]  ~ dmulti(error[true[i], ], nbiops[i])
		}
		error[2,1 : 2] ~ ddirich(prior[1 : 2])
		error[3,1 : 3] ~ ddirich(prior[1 : 3])
		error[4,1 : 4] ~ ddirich(prior[1 : 4])
		p[1 : 4] ~ ddirich(prior[]);     # prior for p
	}

 
Data ( click to open )

Inits for chain 1 		Inits for chain 2	( click to open )

Results 

A 1000 update burn in followed by a further 10000 updates gave the parameter estimates

		mean	sd	MC_error	val2.5pc	median	val97.5pc	start	sample
	error[2,1]	0.589	0.06414	0.001788	0.4618	0.5899	0.7106	1001	10000
	error[2,2]	0.411	0.06414	0.001788	0.2895	0.4101	0.5383	1001	10000
	error[3,1]	0.343	0.04458	7.204E-4	0.2601	0.3413	0.4351	1001	10000
	error[3,2]	0.03664	0.01773	3.001E-4	0.009127	0.03436	0.07722	1001	10000
	error[3,3]	0.6203	0.04678	7.569E-4	0.5246	0.6214	0.7077	1001	10000
	error[4,1]	0.09864	0.04228	6.379E-4	0.03315	0.09341	0.197	1001	10000
	error[4,2]	0.02275	0.02313	3.87E-4	5.184E-4	0.01534	0.08681	1001	10000
	error[4,3]	0.2052	0.05992	0.001004	0.1042	0.2004	0.3357	1001	10000
	error[4,4]	0.6734	0.07257	0.001289	0.5205	0.6774	0.8061	1001	10000
	p[1]	0.1521	0.04878	0.001575	0.04877	0.1534	0.2427	1001	10000
	p[2]	0.3113	0.05377	0.001498	0.2161	0.307	0.4264	1001	10000
	p[3]	0.3888	0.04342	6.389E-4	0.3049	0.388	0.4765	1001	10000
	p[4]	0.1479	0.02961	3.886E-4	0.09444	0.1459	0.2109	1001	10000


