	Seeds: Random effect logistic
			regression

This example is taken from Table 3 of Crowder (1978), and concerns the proportion of seeds that germinated on each of 21 plates arranged according to a 2 by 2 factorial layout by seed and type of root extract.   The data are shown below, where ri and ni are the number of germinated and the total number of seeds on the i th plate, i =1,...,N.  These data are also analysed by, for example, Breslow: and Clayton (1993). 


		seed O. aegyptiaco 75						seed O. aegyptiaco 73
		Bean			Cucumber			Bean			Cucumber
			r	n	r/n	r	n	r/n	r	n	r/n	r	n	r/n
	_________________________________________________________________
	10	39	0.26	5	6	0.83	8	16	0.50	3	12	0.25
	23	62	0.37	53	74	0.72	10	30	0.33	22	41	0.54	
	23	81	0.28	55	72	0.76	8	28	0.29	15	30	0.50
	26	51	0.51	32	51	0.63	23	45	0.51	32	51	0.63
	17	39	0.44	46	79	0.58	0	4	0.00	3	7	0.43
				10	13	0.77	

The model is essentially a random effects logistic, allowing for over-dispersion.  If pi is the probability of germination on the i th plate,  we assume

	ri  ~  Binomial(pi, ni)
	
	logit(pi) = a0 + a1x1i + a2x2i + a12x1ix2i + bi
	
	bi  ~ Normal(0, t)
	
where x1i  , x2i   are the seed type and root extract of the i th plate, and an interaction term a12x1ix2i   is included.   a0 , a1 ,  a2 ,   a12 ,   t  are given independent "noninformative" priors.  

Graphical model for seeds  example

 
BUGS language for seeds example

	
		model
		{
			for( i in 1 : N ) {
				r[i] ~ dbin(p[i],n[i])
				b[i] ~ dnorm(0.0,tau)
				logit(p[i]) <- alpha0 + alpha1 * x1[i] + alpha2 * x2[i] + 
					alpha12 * x1[i] * x2[i] + b[i]
			}
			alpha0 ~ dnorm(0.0,1.0E-6)
			alpha1 ~ dnorm(0.0,1.0E-6)
			alpha2 ~ dnorm(0.0,1.0E-6)
			alpha12 ~ dnorm(0.0,1.0E-6)
			tau ~ dgamma(0.001,0.001)
			sigma <- 1 / sqrt(tau)
		}


Data ( click to open )

Inits for chain 1 	Inits for chain 2	( click to open )


Results

A burn in of 1000 updates followed by a further 10000 updates gave the following parameter estimates:


		mean	sd	MC_error	val2.5pc	median	val97.5pc	start	sample
	alpha0	-0.5499	0.1965	0.004298	-0.9433	-0.5522	-0.1596	1001	10000
	alpha1	0.08902	0.3124	0.005997	-0.5504	0.09795	0.6812	1001	10000
	alpha12	-0.841	0.4372	0.008725	-1.736	-0.8265	0.008258	1001	10000
	alpha2	1.356	0.2772	0.006133	0.8298	1.351	1.914	1001	10000
	sigma	0.2922	0.1467	0.007297	0.04439	0.2838	0.6104	1001	10000


We may compare simple logistic, maximum likelihood (from EGRET), penalized quasi-likelihood  (PQL) Breslow and Clayton (1993) with the BUGS results



Heirarchical centering is an interesting reformulation of random effects models. Introduce the variables

	mi  = a0 + a1x1i + a2x2i + a12x1ix2i  

	bi  =  mi + bi

the model then becomes
	ri  ~  Binomial(pi, ni)

	logit(pi) = bi

	bi  ~ Normal(mi , t)

The graphical model is shown below	


	This formulation of the model has two advantages: the squence of random numbers generated by the Gibbs sampler has better correlation properties and the time per update is reduced because the updating for the a parameters is now conjugate.
