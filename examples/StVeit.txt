	St Veit-Klinglberg, Austria - 
							Radiocarbon calibration with
							stratification

This example is from the book Buck CE, Cavanagh WG & Litton CD (1996) Bayesian approach to interpreting archaeological data.  Wiley: Chichester p218-226
See also Buck CE, Litton CD & Shennan SJ (1994) A case study in combining radiocarbon and archaeological information: the Early Bronze Age settlement of St Veit-Klinglberg, Lan Salzburg, Austria. Germania 2 427-447. The model was set up by Andrew Millard. 
© Andrew Millard 2001 


	model{ 
		theta[1] ~ dunif(theta[2], theta.max)
		theta[2] ~ dunif(theta[3], theta[1])
		theta[3] ~ dunif(theta[9], theta[2])
		theta[4] ~ dunif(theta[9], theta.max)
		theta[5] ~ dunif(theta[7], theta.max)
		theta[6] ~ dunif(theta[7], theta.max)
		theta[7] ~ dunif(theta[9], theta7max)
		theta7max <- min(theta[5], theta[6])
		theta[8] ~ dunif(theta[9], theta.max)
		theta[9] ~ dunif(theta[10], theta9max)
		theta9max <-min(min(theta[3], theta[4]),  min(theta[7], theta[8]))
		theta[10] ~ dunif(theta[11], theta[9])
		theta[11] ~ dunif(0 ,theta[10])
		
		bound[1] <- ranked(theta[1:8],  8)
		bound[2] <- ranked(theta[1:8], 1)
		bound[3] <- ranked(theta[9:11], 3)
		bound[4] <- ranked(theta[9:11], 1)
		
		for (j in 1 : 5){
			theta[j + 11] ~ dunif(0, theta.max)
			within[j, 1] <- 1 - step(bound[1] - theta[j + 11])
			for (k in 2 : 4){
				within[j, k] <- step(bound[k - 1] - theta[j + 11]) 
					- step(bound[k] - theta[j + 11])
			}
			within[j, 5] <- step(bound[4] - theta[j + 11])
		}


		for (i in 1:nDate){
			X[i] ~ dnorm(mu[i], tau[i])
			tau[i] <- 1/pow(sigma[i],2)
			mu[i] <- interp.lin(theta[i], calBP[], C14BP[])

	# monitor the following variable to smooth density of theta
			theta.smooth[i] <- 10 * round(theta[i] / 10)
		}
	}

Data Radio Carbon Calibration Curve ( click to open )

Inits for chain 1 		Inits for chain 2	( click to open )


Results





		mean	sd	
	within[1,1]	0.1903	0.3925	
	within[1,2]	0.7547	0.4303	
	within[1,3]	0.0219	0.1464	
	within[1,4]	0.0306	0.1722	
	within[1,5]	0.0025	0.04994	
	within[2,1]	0.5251	0.4994	
	within[2,2]	0.4706	0.4991	
	within[2,3]	0.0022	0.04685	
	within[2,4]	0.0021	0.04578	
	within[2,5]	0.0	0.0	
	within[3,1]	0.0061	0.07786	
	within[3,2]	0.5417	0.4983	
	within[3,3]	0.0988	0.2984	
	within[3,4]	0.2637	0.4406	
	within[3,5]	0.0897	0.2858	
	within[4,1]	0.0	0.0	
	within[4,2]	0.0444	0.206	
	within[4,3]	0.0314	0.1744	
	within[4,4]	0.2845	0.4512	
	within[4,5]	0.6397	0.4801	
	within[5,1]	0.4511	0.4976	
	within[5,2]	0.5429	0.4982	
	within[5,3]	0.0034	0.05821	
	within[5,4]	0.0026	0.05092	
	within[5,5]	0.0	0.0	

