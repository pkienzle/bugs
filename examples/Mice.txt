	Mice: Weibull regression

Dellaportas and Smith (1993) analyse data from Grieve (1987) on photocarcinogenicity in four groups, each containing 20 mice, who have recorded a survival time and whether they died or were censored at that time.  A portion of the data, giving survival times in weeks, are shown below.  A *  indicates censoring.


	Mouse	Irradiated	Vehicle	Test	Positive
		control	control	substance	control
	________________________________________________________
	1	12	32	22	27
	.......
	18	*40	30	24	12
	19	31	37	37	17
	20	36	27	29	26

The survival distribution is assumed to be Weibull. That is

	f (ti, zi)  = rebzi tir - 1 exp(-ebzitir)
	
where ti is the failure time of an individual with covariate vector  zi and b is a vector of unknown regression coefficients. This leads to a baseline hazard function of the form 

	l0(ti)  = rtir - 1


Setting mi = ebzi gives the parameterisation

	ti  ~ Weibull(t, mi)

For censored observations, the survival distribution is a truncated Weibull, with lower bound corresponding to the censoring time. The regression b coefficients were assumed a priori to follow independent Normal distributions with zero mean and ``vague'' precision 0.0001. The shape parameter r for the survival distribution was given a Gamma(1, 0.0001) prior, which is slowly decreasing on the positive real line.  

Median survival for individuals with covariate vector zi  is given by mi  = (log2e-bzi)1/r

The appropriate graph and BUGS language are below, using an undirected dashed line to represent a logical range constraint. 

		

	model
	{	
		for(i in 1 : M) {
			for(j in 1 : N) {                          
				t[i, j] ~ dweib(r, mu[i])C(t.cen[i, j],)
				cumulative.t[i, j] <- cumulative(t[i, j], t[i, j])
			}
			mu[i] <- exp(beta[i])
			beta[i] ~ dnorm(0.0, 0.001)
			median[i] <- pow(log(2) * exp(-beta[i]), 1/r)  
		}
		r ~ dexp(0.001)
		veh.control <- beta[2] - beta[1]     
		test.sub <- beta[3] - beta[1]
		pos.control <- beta[4] - beta[1]
	}


We note a number of tricks in setting up this model. First, individuals who are censored are given a missing value in the vector of failure times t, whilst individuals who fail are given a zero in the censoring time vector  t.cen (see data file listing below). The truncated Weibull is modelled using C(t.cen[i],) to set a lower bound. Second, we set a parameter beta[j] for each treatment group j. The contrasts beta[j] with group 1 (the irradiated control) are calculated at the end. Alternatively, we could have included a grand mean term in the relative risk model and constrained beta[1] to be zero.


Data	( click to open )

Inits for chain 1 	Inits for chain 2	( click to open )

Results

A burn in of 1000 updates followed by a further 10000 updates gave the parameter estimates

		mean	sd	MC_error	val2.5pc	median	val97.5pc	start	sample
	median[1]	23.77	1.993	0.06917	20.1	23.67	28.03	2001	20000
	median[2]	34.91	3.508	0.1201	28.93	34.59	42.7	2001	20000
	median[3]	26.63	2.371	0.08281	22.32	26.52	31.63	2001	20000
	median[4]	21.32	1.883	0.05613	18.0	21.19	25.4	2001	20000
	pos.control	0.3208	0.3435	0.01172	-0.3314	0.3173	0.977	2001	20000
	r	2.908	0.2981	0.02056	2.371	2.891	3.538	2001	20000
	test.sub	-0.3283	0.3388	0.01176	-0.9825	-0.3333	0.36	2001	20000
	veh.control	-1.108	0.3671	0.01373	-1.842	-1.105	-0.3707	2001	20000

